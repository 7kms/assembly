# 外中断

CPU除了有运算能力外, 还能够对外部设备进行控制(接收输入, 对外输出), 也就是还要有I/O(Input/Output, 输入/输出)能力.

## 接口芯片和端口

CPU通过端口和外部设备进行联系

- 外设的输入不直接送入内存和CPU, 而是送入相关的接口芯片的端口中.
- CPU向外设的输出不是直接送入外设, 而是先送入端口中, 再由相关的芯片送到外设
- CPU还可以向外设输出控制命令

## 外中断信息

当CPU外部有需要处理的信息时, 比如说外设的输入到达, 相关芯片将向CPU发出相应的中断信息. CPU在执行完当前指令后, 可以检测到发送过来的中断信息, 引发中断过程, 处理外设的输入.

在PC系统中, 外中断源有两类:
1. 可屏蔽中断(CPU可以不响应的`外中断`)
    - 当CPU检测到可屏蔽中断信息时, 如果IF=1, 则CPU会响应中断; 如果IF=0,则不响应可屏蔽中断
    - 可屏蔽中断信息来自于CPU外部, 中断类型码是通过数据总线送入CPU的.(内中断的中断类型码是在CPU内部产生的)

中断过程中将IF置为0的原因是, 在进入中断处理程序后, 禁止其他的可屏蔽中断. 如果在中断过程中需要处理可屏蔽中断, 可以用指令将IF置1

8086CPU提供的设置IF的指令:
```s
sti     ;设置IF=1
cli     ;设置IF=0
```

2. 不可屏蔽中断(CPU必须相应的`外中断`), 系统中有必须处理的紧急情况发生时用来通知CPU的中断信息
- CPU检测到不可屏蔽中断信息时, 在执行完当前指令后, 立即响应, 引发中断过程

对于8086CPU,不可屏蔽中断的中断类型码固定为2,所以中断过程中,不需要取中断类型码.

中断过程如下:
1. 标志寄存器入栈, IF=0, TF=0
2. CS,IP入栈
3. (IP)=8, (CS)=(0AH)

几乎所有由外设引发的外中断, 都是可屏蔽中断. 比如说键盘输入发生时, 相关芯片向CPU发出可屏蔽中断信息.

## PC机键盘的处理过程

1. 键盘输入

每当按下和松开一个按键, 都会产生一个扫描码. 按下一个键时产生的扫描码为通码, 松开一个键时产生的扫描码为断码. 扫描码的长度为一个字节, 通码的第7位为0, 断码的第7位为1.`断码=通码+80h`(比如,g键的通码为22h, 断码为a2h). 扫描码被送入主板上的相关接口芯片的寄存器中(端口地址是60h)

2. 引发9号中断
键盘的输入到达60h端口时, 相关的芯片就会向CPU发出中断类型码为9的可屏蔽中断信息. CPU检测到该中断信息后, 如果IF=1, 则响应中断, 引发中断过程, 转去执行int9中断例程.

3. 执行int9中断例程
BIOS提供了int9中断例程, 用来进行基本的键盘输入处理, 主要工作如下:
1. 读出60h端口中的扫描码
2. 如果是字符键的扫描码, 将其对应的字符码(即ASCII码)送入内存中的BIOS键盘缓冲区. 如果是控制键(如ctrl)和切换键(如CapsLock)扫描码, 则将其转变为状态字节(用二进制位记录控制键和切换键状态的字节)写入内存中存储状态字节的单元(`0040:17`)
3. 对键盘系统进行相关的控制, 比如说, 向相关芯片发出应答信息

BIOS键盘缓冲区是系统启动后, BIOS用于存放int9中断例程所接收的键盘输入的内存区(可存储15个键盘输入).int9中断例程除了接收扫描码外, 还要产生和扫描码对应的字符码. 在BIOS键盘缓冲区中, 一个键盘输入用一个`字单元`存放, 高位字节存放扫描码, 低位字节存放字符码

## 编写int9中断例程
键盘输入的处理过程:
1. 键盘产生扫描码
2. 扫描码送入60h端口
3. 引发9号中断
4. cpu执行int9中断例程处理键盘输入
其中1,2,3步都是由硬件系统完成的, 程序员可以改变的只有int9中断例程

编程: 在屏幕中间依次显示"a~z", 并可以让人看清. 在显示的过程中, 按下Esc键后, 改变显示的颜色

```s
assume cs:code
stack segment
  db 128 dup (0)
stack ends

code segment
  start: mov ax,0b800h
         mov es,ax
         mov ah,'a'

      s: mov es:[160*12+40*2],ah
         call delay                   ;为了能看清屏幕上的显示, 需要加上延时
         inc ah
         cmp ah,'z'
         jna s

         mov ax,4c00h
         int 21h


  delay: push ax
         push dx
         mov dx,10h
         mov ax,0
    s1:  sub ax,1
         sbb dx,0
         cmp ax,0
         jne s1
         cmp dx,0
         jne s1
         pop dx
         pop ax
         ret
code ends

end start
```
以上程序已经实现显示`a~z`并可以让人看清. 接下来实现按下ESC之后, 改变显示的颜色

分析:
1. 从60端口读出键盘的输入
    - `in al,60h`

2. 调用BIOS的int9中断例程, 处理其他硬件细节
    - 将中断向量表中的int9中断例程的入口地址改为我们系的中断例程的入口
    - 新的中断例程调用原来的int9中断例程
    
    如要模拟int过程, 主要流程可以分为:
    1. 标志寄存器入栈
    2. IF=0,TF=0
    3. CS,IP入栈
    4. `(IP)=((ds)*16+0), (CS)=((ds)*16+2)`

    其中3,4步和`call dword ptr ds:[0]`的功能一样

    所以int过程的模拟过程变为:
    1. 标志寄存器入栈
         
         ```s
          pushf
         ```
    2. IF=0,TF=0
            
          ```s
            pushf
            pop ax
            and ah,11111100b      ;IF和TF为标志寄存器的第9位和第8位
            push ax
            popf                  ;实现IF=0,TF=0
          ``` 
    3. call dword ptr ds:[0]
        
        ```s
          call dword ptr ds:[0]   ;CS,IP入栈, 且(IP)=((ds)*16+0),(CS)=((ds)*16+2)
        ```

3. 判断是否为ESC的扫描码, 如果是, 改变显示的颜色后返回; 如果不是则直接返回
    - 由于字符显示的位置在屏幕中间(12行40列), 显存的偏移地址是`160*12+40*2`
    - 所以只要改变`160*12+40*2+1`处的数据, 就可以改变字符的颜色
    - 程序返回之前恢复中断向量表中被修改的入口地址, 否则别的程序将无法使用键盘

```s
assume cs:code
stack segment
  db 128 dup (0)
stack ends

data segment
  dw 0,0
data ends

code segment
  start: mov ax,stack
         mov ss,ax
         mov sp,128

         mov ax,data
         mov ds,ax

         mov ax,0
         mov es,ax

         push es:[9*4]
         pop ds:[0]
         push es:[9*4+2]
         pop ds:[2]           ;将原来的int9 中断例程的入口地址保存在ds:0, ds:2单元中

         
         cli     ;设置IF=0, 不响应可屏蔽中断. 让随后的2条命令可以连续执行, 保证新的9号中断例程入口地址的正确性
         mov word ptr es:[9*4],offset int9
         mov es:[9*4+2],cs    ;在中断向量表中设置新的int9中断例程的入口地址
         sti     ;设置IF=1, 响应可屏蔽中断

         mov ax,0b800h
         mov es,ax
         mov ah,'a'

     s:  mov es:[160*12+40*2],ah
         call delay
         inc ah
         cmp ah,'z'
         jna s

         mov ax,0
         mov es,ax
         push ds:[0]
         pop es:[9*4]
         push ds:[2]
         pop es:[9*4+2]       ;将中断向量表中int9中断例程的入口恢复为原来的地址

         mov ax,4c00h
         int 21h

  delay: push ax
         push dx
         mov dx,10h
         mov ax,0
    s1:  sub ax,1
         sbb dx,0
         cmp ax,0
         jne s1
         cmp dx,0
         jne s1
         pop dx
         pop ax
         ret

;------------以下为新的int9中断例程-------

    int9: push ax
          push bx
          push es

          in al,60h
          pushf

          pushf
          pop bx
          and bh,11111100b          ;IF和TF为标志寄存器的第9位和第8位
          push bx
          popf
          call dword ptr ds:[0]     ;对int指令进行模拟, 调用原来的int9中断例程

          cmp al,1
          jne int9ret

          mov ax,0b800h
          mov es,ax
          inc byte ptr es:[160*12+40*2+1]   ;将属性值加1, 改变颜色

  int9ret: pop es
           pop bx
           pop ax
           iret

code ends

end start

```

## 安装新的int9中断例程
- 任务: 安装一个新的int9中断例程
- 功能: 在DOS下, 按F1键后改变当前屏幕的显示颜色, 其他的键照常处理

分析:
1. 改变屏幕的显示颜色
    
    ```s
        ;改变b8000h开始的4000个字节中的所有奇地址单元中的内容.
        mov ax,0b800h
        mov es,ax
        mov bx,1
        mov cx,2000
     s: inc byte ptr es:[bx]
        add bx,2
        loop
    ```
2. 其他键照常处理
    - 直接调用原int9中断处理程序, 来处理其他的键盘输入
3. 原int9中断例程入口地址保存
    - 不能保存在安装程序中, 因为安装程序返回后地址将丢失
    - 可以将地址保存在`0:200`单元处(占4个字节), 
4. 新int9中断例程的安装
    - 由于`0:200`处用来保存原int9地址, 所以新中断例程可以安装到`0:204`处

```s
assume cs:code

stack segment
  db 128 dup (0)
stack ends

code segment
  start: mov ax,stack
         mov ss,ax
         mov sp,128

         push cs
         pop ds

         mov ax,0
         mov es,ax

         mov si,offset int9                 ;设置ds:si指向源地址
         mov di,204h                        ;设置es:di指向目标地址
         mov cx,offset int9end-offset int9  ;设置cx为传输长度
         cld                                ;设置传输方向为正
         rep movsb

         push es:[9*4]
         pop es:[200h]
         push es:[9*4+2]
         pop es:[202h]      ;暂存BIOS原有的int9中断例程地址

         cli
         mov word ptr es:[9*4],204h
         mov word ptr es:[9*4+2],0  ;设置新的int9中断例程的地址0:204
         sti    

         mov ax,4c00h
         int 21h   

  int9:  push ax
         push bx
         push cx
         push es

         in al,60h
         pushf
         call dword ptr cs:[200h]

         cmp al,3bh     ;F1的扫描码 3bh
         jne int9ret

         mov ax,0b800h
         mov es,ax
         mov bx,1
         mov cx,2000
    s:   inc byte ptr es:[bx]
         add bx,2
         loop s

int9ret: pop es
         pop cx
         pop bx
         pop ax
         iret

int9end: nop

code ends
end start
```

CPU对外设输入的通常处理方法:
1. 外设的输入送入端口
2. 向CPU发出外中断(可屏蔽中断)信息
3. CPU检测到可屏蔽中断信息, 如果IF=1, CPU在执行完当前指令后响应中断, 执行相应的中断例程
4. 可在中断例程中实现对外设输入的处理
端口和中断机制, 是CPU进行I/O的基础

## 实验15 安装新的int9中断例程
功能: 在DOS下, 按下"A"键后, 除非不在松开, 如果松开, 就显示满屏幕的A, 其他的键照常处理

分析:
- 按下一个键时产生的扫描码为通码, 松开一个键产生的扫描码称为断码
- 断码=通码+80h
- A键的通码是1Eh, 断码是: 1Eh+80h=9Eh

```s
assume cs:code

stack segment
  db 128 dup (0)
stack ends

code segment
  start: mov ax,stack
         mov ss,ax
         mov sp,128

         push cs
         pop ds

         mov ax,0
         mov es,ax

         mov si,offset int9                 ;设置ds:si指向源地址
         mov di,204h                        ;设置es:di指向目标地址
         mov cx,offset int9end-offset int9  ;设置cx为传输长度
         cld                                ;设置传输方向为正
         rep movsb

         push es:[9*4]
         pop es:[200h]
         push es:[9*4+2]
         pop es:[202h]      ;暂存BIOS原有的int9中断例程地址

         cli
         mov word ptr es:[9*4],204h
         mov word ptr es:[9*4+2],0  ;设置新的int9中断例程的地址0:204
         sti    

         mov ax,4c00h
         int 21h   

  int9:  push ax
         push bx
         push cx
         push es

         in al,60h
         pushf
         call dword ptr cs:[200h]

         cmp al,9Eh     ;A的断码9Eh
         jne int9ret

         mov ax,0b800h
         mov es,ax
         mov bx,0
         mov cx,2000
    s:   mov es:[bx], 'A'
         add bx,2
         loop s

int9ret: pop es
         pop cx
         pop bx
         pop ax
         iret

int9end: nop

code ends
end start
```

# 指令系统总结
8086CPU提供一下几大类指令

1. 数据传送指令
    - 如: mov,push,pop,pushf,popf,xchg等都是数据传送指令
    - 这些指令实现寄存器和内存,寄存器和寄存器直接的单个数据传送

2. 算数运算指令
    - 如: add,sub,adc,sbb,inc,dec,cmp,imul,idiv,aaa等都是算数运算指令
    - 这些指令实现寄存器和内存中的数据的算数运算
    - 他们的执行结果影响标志寄存器的sf,zf,of,cf,pf,af位

3. 逻辑指令
    - 如: and,or,not,xor,test,shl,shr,sal,sar,rol,ror,rcl,rcr等都是逻辑指令
    - 除了not指令外, 他们的执行结果都影响标志寄存器的相关标志位

4. 转移指令
    - 可以修改IP, 或同时修改CS和IP的指令
    - 无条件转移指令: jmp
    - 条件转移指令: jcxz,je,jb,ja,jnb,jna等
    - 循环指令: loop
    - 过程: call,ret,retf
    - 中断: int,iret

5. 处理机控制指令
    - 对标志寄存器或其他处理机状态进行设置
    - 如: cld,std,cli,sti,nop,clc,cmc,stc,hlt,wait,esc,lock等

6. 串处理指令
    - 对内存中的批量数据进行处理
    - 如: movsb,movsw,cmps,scas,lods,stos等
    - 若要使用这些指令方便的进行批量数据的处理, 则需要和rep,repe,prepne等前缀指令配合使用